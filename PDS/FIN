         MACRO
&NOMB    FIN   &CR,               CODIGO DE RETORNO (SOLO NORENT)      X
               &TIPO=NORENT       TIPO (NORENT,RENT,ERROR)
         GBLA  &SBP,&SBA,&CAP,&SAC,&SAV
.*
.* ****OS3906A*********************************************************
.* *                                                                  *
.* *   MACRO FIN    PARA PROGRAMAS ASSEMBLER                          *
.* *                                                                  *
.* *------------------------------------------------------------------*
.* *                                                                  *
.* *   TECNICA DE SISTEMAS                                            *
.* *   ANGEL LUIS DOMINGUEZ MARTIN                                    *
.* *   16 DE JULIO DE 1.983, SEPTIEMBRE DE 1.992, MAYO DE 1.996       *
.* *                                                                  *
.* *------------------------------------------------------------------*
.* *                                                                  *
.* * ESTA MACRO CONSERVA LAS FUNCIONES DE LA ANTIGUA FIN PARA PGMS.   *
.* * NO-REENTRANTES, INCOPORANDO FUNCIONES PARA LA CODIFICACION DE    *
.* * RUTINAS REENTRANTES.                                             *
.* *                                                                  *
.* * SU COMPLEMENTARIA, LA MACRO  -INICIO- HA SIDO ACTUALIZADA PARA   *
.* * ADMITIR NUEVAS FUNCIONES ACORDE CON ESTA.                        *
.* *                                                                  *
.* * PARAMETROS:                                                      *
.* * ----------                                                       *
.* *                                                                  *
.* * &CR      POSICIONAL  - OBLIGATORIO SI TIPO=NORENT                *
.* *                      - PARA MANTENER LA ANTIGUA ESTRUCTURA.      *
.* *                      - FIGURARA OBLIGATORIAMENTE LA CONST. -CR-  *
.* *                                                                  *
.* * TIPO     CLAVE       - PUEDE TENER VALORES NORENT/RENT/ERRGET    *
.* *                      - POR DEFECTO TOMA  -NORENT-                *
.* *                      - INDICA NO-REENTRANTE O REENTRANTE.        *
.* *                                                                  *
.* *          NORENT      - DEVOLUCION PARA NO REENTRANTES.           *
.* *                      - TOMA EL CODIGO DE RETORNO DE LA VARIABLE  *
.* *                        CR DEFINIDA EN LA MACRO INICIO.           *
.* *                                                                  *
.* *          RENT        - DEVOLUCION PARA REENTRANTES.              *
.* *                      - TOMA EL CODIGO DE RETORNO DE  - R15 -.    *
.* *                      - EFECTUA FREEMAIN DEL AREA DINAMICA.       *
.* *                                                                  *
.* *          ERRGET      - DEVOLUCION PARA REENTRANTES CUYA GETMAIN  *
.* *                        HAYA DEVUELTO CODIGO DE ERROR.            *
.* *                      - TIENE EN CUENTA QUE NO HAN SIDO COMPLE-   *
.* *                        TADOS LOS ENCADENAMIENTOS STANDARD DE LAS *
.* *                        SAVE AREAS AL FALLAR LA GETMAIN.          *
.* *                                                                  *
.* ***************************************** DOMINGUEZ   JULIO-1983 ***
* ---------------------------------------------------------------------
* *   V.S.I.             DEVOLUCION DE CONTROL PROGRAMAS O.S.         *
* ---------------------------------------------------------------------
         AIF   ('&NOMB' EQ '').LB00
&NOMB    EQU   *
.LB00    ANOP
         AIF   ('&TIPO' EQ 'ERRGET').LBERR
.*
         AIF   ('&TIPO' EQ 'RENT').LBRENT
.*
         AIF   ('&TIPO' NE 'NORENT').TIPO1
         AIF   ('&SAC' NE '1').CR001
         AIF   ('&CR' NE 'CR').CR1
         AIF   (&SAV EQ 0).CR002
         L     R13,SAVE+4         CARGA DIRECCION DE SAVE AREA ANTERIOR
         AGO   .CR002
.CR001   ANOP
         AIF   ('&CR' NE 'CR&SAC').CR1
         AIF   (&SAV EQ 0).CR002
         L     R13,SAVE&SAC+4     CARGA DIRECCION DE SAVE AREA ANTERIOR
.CR002   ANOP
         SR    R15,R15            LIMPIAR CODIGO DE RETORNO
         IC    R15,&CR            CARGA CODIGO DE RETORNO
         AGO   .LBERR
.* ------------------- REENTRANTES
.LBRENT  ANOP
         LR    R1,R13             DIRECCION AREA DINAMICA
         L     R0,=A(@LGDIN&SAC)  TAMA#O DEL AREA DINAMICA
         LA    R1,0(,R1)          LIMPIAR OCTETO IZDO. REG. 1
         AIF   (&SAV EQ 0).LBSAV01
         L     R13,4(,R13)        CARGA DIRECCION DE SAVE AREA ANTERIOR
.LBSAV01 ANOP
         LR    R2,R15             SALVA CODIGO DE RETORNO A REG. 2
         AIF   (&SBP EQ 0).LBRENT1
         FREEMAIN R,LV=(0),A=(1),SP=&SBP  EFECTUAR FREEMAIN
         AIF   (&SBP LE 127).LBRENT2
         AIF   (&SBA EQ 1).LBRENT2
         MODESET  KEY=NZERO,MODE=PROB
         AUTORIZA AUTHOFF
         AGO   .LBRENT2
.LBRENT1 ANOP
         FREEMAIN R,LV=(0),A=(1)  EFECTUAR FREEMAIN
.LBRENT2 ANOP
         LR    R15,R2             CARGA CODIGO DE RETORNO
.LBERR   ANOP
         AIF   (&SAV EQ 0).LBSAV02
         L     R14,12(R13)        RESTAURAR DIRECCION DE RETORNO
         LM    R0,R12,20(R13)     RESTAURAR SAVE AREA ANTERIOR
.LBSAV02 ANOP
         AIF   (&CAP EQ 0).LBCAP01
         BSM   R0,R14             DEVOLUCION DE CONTROL CON CAPPING
* ---------------------------------------------------------------------
         AGO   .FIN
.LBCAP01 ANOP
         BR    R14                DEVOLUCION DE CONTROL
* ---------------------------------------------------------------------
         AGO   .FIN
.*
.CR1     MNOTE 255,'INCORRECTA ESPECIFICACION DE CONSTANTE -CR- '
         MEXIT
.TIPO1   MNOTE 255,'INCORRECTA ESPECIFICACION DE -TIPO- '
         MEXIT
.FIN     ANOP
         MEND
