         TITLE '** MDTPUT ** EDITAR EN PANTALLA UN TEXTO POR LIN. COL'
* *****************   MDTPUT    ***************************************
* *                                                                   *
* * NOMBRE ....... MDTPUT   (ANTIGUO TPUTPGM)                         *
* *                                                                   *
* * UBICACION .... SYST.LINKLIB                                       *
* *                                                                   *
* * AUTOR ........ JOSE IGNACIO SANCHEZ GACITUAGA.                    *
* *                ANGEL LUIS DOMINGUEZ MARTIN (REVISION/IMPLEMENTAC) *
* *                                                                   *
* * FUNCIONES .... EDITAR TEXTO EN PANTALLA.                          *
* *                                                                   *
* * OBJETIVOS .... EDITAR EN PANTALLA UN TEXTO EN UNA LINEA Y COLUMNA *
* *                ESPECIFICADA EN PARM.                              *
* *                                                                   *
* * LENGUAJE ..... HIGH LEVEL ASSEMBLER                               *
* *                                                                   *
* * TIPO ......... MODULO ESTATICO.                                   *
* *                                                                   *
* * PARAMETROS ... UN UNICO PARAMETRO CON LOS DATOS SIGUIENTES:       *
* *                                                                   *
* *            1 O 2  OCTETOS  NUMERO DE LINEA. (MENOR DE 24)         *
* *            1 O 2     "     NUMERO DE COLUMNA.(MENOR DE 80)        *
* *                X     "     TEXTO A EDITAR.                        *
* *****************   TPUT ********************************************
         EJECT
MDTPUT   INICIO 12
         L      R2,0(R2)
         SR     R4,R4
         LH     R4,0(R2)
         LTR    R4,R4
         BZ     NODATOS
*-----------TOMAR NUMERO DE LINEA-------------
         SR     R7,R7
         LA     R5,LIN
         LA     R6,2(R2)
BUC      CLI    0(R6),C','
         BE     P1
         MVC    0(1,R5),0(R6)
         LA     R5,1(R5)
         LA     R6,1(R6)
         LA     R7,1(R7)
         BCT    R4,BUC
P1       STH    R7,LLIN
         BCTR   R7,R0
         EX     R7,PACK1
         CVB    R6,PP
         CH     R6,=H'24'
         BC     2,HLIN
         BCTR   R6,R0
         MH     R6,=H'80'
*----------------------------------------------
*-----------TOMAR NUMERO DE COLUMNA-----------
         SR     R7,R7
         LA     R5,COL
         LA     R8,2(R2)
         LH     R7,LLIN
         LA     R8,1(R7,R8)
         SR     R7,R7
BUC1     CLI    0(R8),C','
         BE     P2
         MVC    0(1,R5),0(R8)
         LA     R5,1(R5)
         LA     R8,1(R8)
         LA     R7,1(R7)
         BCT    R4,BUC1
P2       STH    R7,LCOL
         BCTR   R7,R0
         EX     R7,PACK2
         CVB    R8,PP
         CH     R8,=H'80'
         BC     2,HCOL
         BCTR   R8,R0
*----------------------------------------------
*-----------CALCULAR LINEA Y COLUMNA----------
         AR     R6,R8
         STH    R6,DATOS+2
         OI     DATOS+2,X'00'
*-----------TOMAR TEXTO ----------------------
         LA     R6,2(R2)
         SR     R7,R7
         SR     R8,R8
         LH     R7,LLIN
         LH     R8,LCOL
         LA     R6,1(R7,R6)
         LA     R6,1(R8,R6)
         LH     R4,0(R2)
         SR     R4,R7
         SR     R4,R8
         BCTR   R4,R0
         BCTR   R4,R0
         BCTR   R4,R0
         EX     R4,MVCPARM
*----------------------------------------------
*-----CONVERSION DE DIRECCION LIN,COL (SBA)----
         LA     R5,DATOS+2
         LH     R7,0(R5)
         STC    R7,TRA+1
         SRL    R7,6
         STC    R7,TRA
         NI     TRA+1,X'3F'
         TR     TRA(2),TAB
         MVC    DATOS+2(2),TRA
*----------------------------------------------
*-----LANZAR MDTPUT EN LIN, COL-----------------
         LA     R4,LDATOS+1(R4)
         TPUT   DATOS,(R4),FULLSCR
         LA     R5,20
         CR     R15,R5
         BH     FIN
         B      IRA(R15)          DUMMY DE BIFURCACION
IRA      B      FIN               BIFURCA SI RC15=0
         B      FIN               BIFURCA SI RC15=4
         B      FIN               BIFURCA SI RC15=8
         B      RCC               BIFURCA SI RC15=12
         B      RC10              BIFURCA SI RC15=16
         B      RC14              BIFURCA SI RC15=20
NODATOS  MVC    DATOS(30),=C'**** NO HAY DATOS EN PARM.    '
         B      TPT
RCC      MVC    DATOS(30),=C'**** TERMINAL CON NOINTERCOM. '
         B      TPT
RC10     MVC    DATOS(30),=C'**** PARAMETROS INVAL. MDTPUT'
         B      TPT
RC14     MVC    DATOS(30),=C'**** TERMINAL DESCONECTADO.   '
         B      TPT
HLIN     MVC    DATOS(30),=C'**** LINEA MAYOR DE 24        '
         B      TPT
HCOL     MVC    DATOS(30),=C'**** COLUMNA MAYOR DE 80      '
TPT      TPUT   DATOS,30,,,,,HIGHP
FIN      FIN    CR
MVCPARM  MVC    DATOS+4(*-*),0(R6)
PACK1    PACK   PP(8),LIN(*-*)
PACK2    PACK   PP(8),COL(*-*)
         DS     0D
DATOS    DC     X'C1'               WCC
         DC     X'114040'           SBA EN LIN 1, COL 1
LDATOS   EQU    *-DATOS
         DC     CL252' '
*
TRA      DC     X'0000'
TAB      DC     X'40C1C2C3C4C5C6C7C8C94A4B'
         DC     X'4C4D4E4F50D1D2D3D4D5D6D7'
         DC     X'D8D95A5B5C5D5E5F6061E2E3'
         DC     X'E4E5E6E7E8E96A6B6C6D6E6F'
         DC     X'F0F1F2F3F4F5F6F7F8F97A'
         DC     X'7B7C7D7E7F'
*
         DS     0D
PP       DC     D'0'
P        DC     P'0'
LLIN     DS     H
LIN      DC     CL4' '
LCOL     DS     H
COL      DC     CL4' '
         LTORG
         END
