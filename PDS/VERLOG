PROC 0 V(O) HLQDS(OPERLOG)
 /********************************************************************/
 /**                                                                **/
 /**  NOMBRE .- VERLOG.                                             **/
 /**  AUTORES.- JOSE MARIA MARTINEZ BESTEIRO                        **/
 /**         .- ANGEL LUIS DOMINGUEZ                                **/
 /**  FUNCION.- VISUALIZACION SELECTIVA DEL SYSLOG/OPERLOG          **/
 /**  DEPARTAMENTOS USUARIOS.- ORGANIZACION E INFORMATICA.          **/
 /**                                                                **/
 /********************************************************************/
TSOCLEAR
CONTROL END(ZZZZ)   MAIN NOMSG NOLIST NOCONLIST NOSYMLIST
IF &STR(&SYSISPF) NE ACTIVE +
  THEN DO
        WRITE *** USTED NO ESTA BAJO ISPF. TECLEE EL COMANDO BAJO ISPF.
        GOTO FIN
       ZZZZ
IF &V NE &STR(O) +
  THEN DO
         ISPEXEC LIBDEF ISPPLIB DATASET ID('SYS4.VPW.ISPPLIB')
         ISPEXEC LIBDEF ISPMLIB DATASET ID('SYS4.VPW.ISPMLIB')
         ISPEXEC LIBDEF ISPSLIB DATASET ID('SYS4.VPW.ISPSLIB')
         ISPEXEC LIBDEF ISPTLIB DATASET ID('SYS4.VPW.ISPTLIB')
         %$SYSPROC LIB(SYS4.VPW.CLIST) OPE(F)
         %VPWCLI1
         ISPEXEC LIBDEF ISPPLIB DATASET ID()
         ISPEXEC LIBDEF ISPMLIB DATASET ID()
         ISPEXEC LIBDEF ISPSLIB DATASET ID()
         ISPEXEC LIBDEF ISPTLIB DATASET ID()
         %$SYSPROC LIB(SYS4.VPW.CLIST) OPE(L)
       ZZZZ
  ELSE DO
         SET &PREFLOG = &STR(&HLQDS)&STR(.)&STR(&SYSPLEX)
         SET &PREFLOG = &SUBSTR(1:&LENGTH(&PREFLOG),&PREFLOG)

         ISPEXEC VGET (ZSCREEN)
         SET &LD  = &ZSCREEN

         IF &STR(&SYSPREF) = &STR(&SYSUID) -
         OR &STR(&SYSPREF) = &STR() -
          THEN SET PREFUSR = &STR(&SYSUID)
          ELSE SET PREFUSR = &STR(&SYSPREF..&SYSUID)

         FREE    FI(FSESLOG&LD)
         DELETE '&PREFUSR&STR(.SPF.VLOG&LD..&SYSNAME)'

         ALLOC   FI(FSESLOG&LD) +
                 DS('&PREFUSR&STR(.SPF.VLOG&LD..&SYSNAME)') +
                 NEW CATALOG SPACE(1 1) CYL UNIT(SYSALLDA) +
                 RECFM(V B) LRECL(4096) BLKSIZE(8196)

         /* ATENCION:  &STR(FSESLOG&LD)  SIEMPRE OCHO POSICIONES */
         /* LA OCTAVA &LD ES USADA POR EL PROGRAMA               */

         SET &PARAME = &STR(&PREFUSR)+
                       &STR(.SPFOLOG,)+
                       &STR(FSESLOG&LD)+
                       &STR(,)+
                       &STR(&PREFLOG)

         ISPEXEC SELECT  PGM(SYVERLOG) +
                 PARM(&PARAME)

         FREE    FI(FSESLOG&LD)

         DELETE '&PREFUSR&STR(.SPF.VLOG&LD..&SYSNAME)'

       ZZZZ
FIN: END
