         MACRO
&NOMBRE  INICIO &R,                     REGISTROS BASE                 X
               &TIPO=NORENT,            TIPO NORENT - RENT             X
               &COMEN=,                 BREVE COMENTARIO DE FUNCIONES  X
               &VERSION=,               VERSION                        X
               &PDIN=,                  NOMBRE DSECT AREA DINAMICA     X
               &FDIN=,                  NOMBRE EQU FIN AREA DINAMICA   X
               &SUBPOOL=,               NUMERO DE SUBPOOL              X
               &ERRGET=,                LABEL PARA ERROR EN GETMAIN    X
               &AMODE=,                 ADRESABILITY                   X
               &RMODE=,                 RESIDENCE                      X
               &AUTH=SI,                SI AUTORIZACION                X
               &CAPPING=SI,             ENLACE ENTRE RUTINAS           X
               &SAVE=SI,                PARA SALVAR REG. A LA ENTRADA  X
               &EQUREG=SI,              PARA DEFINIR EQU DE REGISTROS  X
               &LOC=                    LOCATION PARA GETMAIN
.*
.* **** OS3906A *******************************************************
.* *                                                                  *
.* *   MACRO INICIO PARA PROGRAMAS ASSEMBLER                          *
.* *                                                                  *
.* *------------------------------------------------------------------*
.* *                                                                  *
.* *   TECNICA DE SISTEMAS                                            *
.* *   ANGEL LUIS DOMINGUEZ MARTIN                                    *
.* *   16 DE JULIO DE 1.983, SEPTIEMBRE DE 1.992                      *
.* *                                                                  *
.* * ESTA MACRO CONSERVA LAS FUNCIONES DE LA ANTIGUA INICIO PARA PGMS.*
.* * NO-REENTRANTES, INCOPORANDO FUNCIONES PARA LA CODIFICACION DE    *
.* * RUTINAS REENTRANTES.                                             *
.* * ---------------------------------------------------------------  *
.* * NOTA ===>  SE RECOMIENDA, POR STANDARIZACION, ANTEPONER EL PRE-  *
.* *            FIJO ARROBA (@) A TODAS LAS AREAS QUE SE DEFINAN EN   *
.* *            EN EL ESPACIO DINAMICO.                               *
.* * ---------------------------------------------------------------  *
.* * SU COMPLEMENTARIA, LA MACRO  -FIN-  HA SIDO ACTUALIZADA PARA     *
.* * ADMITIR NUEVAS FUNCIONES ACORDE CON ESTA.                        *
.* *                                                                  *
.* * PARAMETROS:                                                      *
.* * ----------                                                       *
.* *                                                                  *
.* * R        POSICIONAL  - REGISTRO/S ESPECIFICADOS PARA BASE DEL    *
.* *                        PROGRAMA; SIN LA NOTACION RX, HASTA UN    *
.* *                        MAXIMO DE 10.                             *
.* *                      - ES OBLIGATORIO AL MENOS ESPECIFICAR       *
.* *                        UNO, Y SI HUBIERA MAS, IRAN ENTRE         *
.* *                        PARENTESIS Y SEPARADOS POR COMAS.         *
.* *                                                                  *
.* * TIPO     CLAVE       - PUEDE TENER LOS VALORES NORENT / RENT.    *
.* *                      - POR DEFECTO TOMA  -NORENT-                *
.* *                      - INDICA NO-REENTRANTE O REENTRANTE.        *
.* *                                                                  *
.* * COMEN    CLAVE       - NO ES OBLIGATORIO, PERO SE RECOMIENDA SU  *
.* *                        INCLUSION PARA COMENTARIO EN EL PROGRAMA  *
.* *                        TANTO EN FUENTE COMO EN CARGABLE Y GENE-  *
.* *                        RACION DE INSTRUCCION  -TITLE-  PARA EL   *
.* *                        LISTADO DE LA COMPILACION.                *
.* *                      - MAXIMO DE CUARENTA POSICIONES ALFANUMERI- *
.* *                        CAS Y SIEMPRE ENTRE COMILLAS.             *
.* *                                                                  *
.* * VERSION  CLAVE       - NO ES OBLIGATORIO, PERO SE RECOMIENDA SU  *
.* *                        INCLUSION PARA QUE FIGURE LA VERSION      *
.* *                        TANTO EN FUENTE COMO EN CARGABLE.         *
.* *                      - MAXIMO DE SIETE POSICIONES ALFANUMERICAS  *
.* *                                                                  *
.* * PDIN     CLAVE       - OBLIGATORIO SI TIPO=RENT.                 *
.* *                      - ES EL NOMBRE DE LA DSECT QUE VAMOS A UTI- *
.* *                        LIZAR PARA USO DEL AREA DINAMICA.         *
.* *                      - LAS 18 PALABRAS INICIALES DE ESTE AREA    *
.* *                        SE RESERVAN PARA SAVEAREA.                *
.* *                                                                  *
.* * FDIN     CLAVE       - OBLIGATORIO SI TIPO=RENT.                 *
.* *                      - ES EL NOMBRE DE UNA -EQU- QUE DEBEMOS CO- *
.* *                        LOCAR AL FINAL DE NUESTRO AREA DINAMICA.  *
.* *                      - SE UTILIZA PARA CALCULO DEL TAMANO DE AREA*
.* *                        PARA GETMAIN.                             *
.* *                                                                  *
.* * SUBPOOL  CLAVE       - PARAMETRO OPCIONAL.                       *
.* *                      - SOLO SE TIENE EN CUENTA SI TIPO=RENT.     *
.* *                      - NUMERICO Y TRES POSICIONES OBLIGATORIAS EN*
.* *                        CASO DE CONSIGNARSE.                      *
.* *                      - SE UTILIZARA CUANDO SE DESEE EFECTUAR LA  *
.* *                        GETMAIN SOBRE UN SUBPOOL ESPECIAL.        *
.* *                                                                  *
.* * ERRGET   CLAVE       - OBLIGATORIO SI TIPO=RENT.                 *
.* *                      - LABEL DONDE SE BIFURCARA EN CASO DE ERROR *
.* *                        EN LA GETMAIN.                            *
.* *                                                                  *
.* *                                                                  *
.* * AMODE    CLAVE       - PARAMETRO OPCIONAL                        *
.* *                                                                  *
.* * RMODE    CLAVE       - PARAMETRO OPCIONAL                        *
.* *                                                                  *
.* * AUTH     CLAVE       - PARAMETRO OPCIONAL                        *
.* *                      - SI SE QUIERE DAR MACRO AUTORIZA Y MODESET *
.* *                        PARA PEDIR UNA GETMAIN EN SUBPOOL         *
.* *                        ESPECIAL DE MEMORIA.                      *
.* *                                                                  *
.* * CAPPING  CLAVE       - PARAMETRO OPCIONAL                        *
.* *                      - ENLACE ENTRE RUTINAS EN DIFERENTES MODOS. *
.* *                        POR DEFINICION PUEDEN RECIBIR CONTROL EN  *
.* *                        CUALQUIER MODO. OJO, EL MODULO DEBE RESI- *
.* *                        DIR BAJO LOS 16MB SI QUIERE LLAMADAS DE   *
.* *                        USUARIOS EN 24 BITS.                      *
.* *                                                                  *
.* * SAVE     CLAVE       - PARAMETRO OPCIONAL                        *
.* *                      - SI SE QUIERE SALVAR LOS REGISTRO DE ENTRA *
.* *                        A LA RUTINA EN UN SAVE AREA PROPORCIONADA *
.* *                        EN EL REGISTRO 13.                        *
.* *                                                                  *
.* * EQUREG   CLAVE       - PARAMETRO OPCIONAL                        *
.* *                      - PARA DEFINIR LOS EQU DE LOS REGISTROS     *
.* *                        GENERALES.                                *
.* *                                                                  *
.* * LOC      CLAVE       - PARAMETRO OPCIONAL                        *
.* *                      - RESIDENCIA POR ENCIMA O DEBAJO DE 16 MB.  *
.* *                        DEL AREA OBTENIDA POR GETMAIN INICIAL.    *
.* *                                                                  *
.* * ERRORES:                                                         *
.* * -------                                                          *
.* *                                                                  *
.* * - EN CASO DE ERROR EN GETMAIN, SE TENDRA EN CUENTA QUE LA CESION *
.* *   SE EFECTUA SIN COMPLETAR LOS ENCADENAMIENTOS STANDARD DE SAVES *
.* *   POR LO QUE SE PONDRA ESPECIAL CUIDADO EN LAS INSTRUCCIONES A   *
.* *   REALIZAR, FINALIZANDO CON MACRO    -FIN   CR,TIPO=ERROR -      *
.* *                                                                  *
.* ***************************************** DOMINGUEZ   JULIO-1983 ***
         LCLA  &INIA,&INID
         LCLC  &INIC
         GBLA  &SBP,&SBA,&CAP,&SAC,&SAV
.*
&SBP     SETA  0
&SBA     SETA  0
&CAP     SETA  0
&SAV     SETA  0
.*
         AIF   ('&NOMBRE' EQ '').NOMBRE1
         AIF   ('&NOMBRE'(1,1) LT 'A').NOMBRE2
         AIF   ('&NOMBRE'(1,1) GT 'Z').NOMBRE2
         AIF   (K'&NOMBRE GT 8).NOMBRE3
         AIF   (N'&R EQ 0 OR N'&R GT 10).R1
         AIF   ('&R(1)' EQ '').R2
         AIF   (T'&R(1) NE 'N').R3
         AIF   ('&TIPO' EQ 'RENT').LB0
         AIF   ('&TIPO' NE 'NORENT').TIPO1
.LB0     ANOP
         AIF   ('&SUBPOOL' EQ '').LB1
         AIF   (K'&SUBPOOL GT 3).SUBPOO1
         AIF   (T'&SUBPOOL NE 'N').SUBPOO2
         AIF   (&SUBPOOL GT 255).SUBPOO3
         AIF   (&SUBPOOL LE 127).LB1
         MNOTE 000,'----------------------------------------------'
         MNOTE 000,' --NOTA--  SUBPOOL SOLICITADO SUPERIOR A 127. '
         MNOTE 000,'----------------------------------------------'
.LB1     ANOP
         AIF   ('&COMEN' EQ '').LB2
         AIF   (K'&COMEN GT 40).COMEN1
         AIF   ('&COMEN'(1,1) NE '''').COMEN2
&INIA    SETA  K'&COMEN
&INIA    SETA  &INIA-2
&INIC    SETC  '&COMEN'(2,&INIA)
.LB2     ANOP
         AIF   ('&TIPO' EQ 'NORENT').LB3
         AIF   ('&PDIN' EQ '').PDIN1
         AIF   ('&FDIN' EQ '').FDIN1
.LB3     ANOP
* ---------------------------------------------------------------------
* *   V.S.I.             INICIALIZACION DE PROGRAMAS O.S.             *
* ---------------------------------------------------------------------
&NOMBRE  CSECT
.*
         AIF   ('&AMODE' EQ '').AM1
&NOMBRE  AMODE &AMODE
.AM1     ANOP
         AIF   ('&RMODE' EQ '').RM1
&NOMBRE  RMODE &RMODE
.RM1     ANOP
         AIF   ('&EQUREG' NE 'SI').EQ1
R0       EQU   0
R1       EQU   1                  ----- ATENCION -----
R2       EQU   2
R3       EQU   3
R4       EQU   4                  DEVUELVE LA LISTA DE PARAMETROS
R5       EQU   5
R6       EQU   6                  DIRECCIONADA EN  --  R2  --.
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
.EQ1     ANOP
.*
         USING &NOMBRE,R15
         B     L&SYSNDX
         DC    AL1(I&SYSNDX-*)
         DC    CL8'&NOMBRE'
         DC    CL8'_&VERSION'
         DC    C'_BBVA'
         DC    C'(&SYSDATC./&SYSTIME./&SYSJOB)'
         AIF   ('&COMEN' EQ '').LB00A
         DC    C'_&INIC'
.LB00A   ANOP
         DC    C'_&SYSASM._&SYSVER.'
&SAC     SETA  &SAC+1
I&SYSNDX EQU   *
         AIF   ('&TIPO' EQ 'RENT').LB00B
         AIF   ('&SAC' NE '1').LB00D
SAVE     DS    18F
CR       DC    X'00'
         AGO   .LB00C
.LB00D   ANOP
SAVE&SAC DS    18F
CR&SAC   DC    X'00'
         AGO   .LB00C
.LB00B   ANOP
@LGDIN&SAC EQU &FDIN-&PDIN        LONGITUD EN OCTETOS AREA DINAMICA
.LB00C   ANOP
L&SYSNDX DS    0H
.*
         AIF   ('&SAVE' NE 'SI').SA1
&SAV     SETA  1
         STM   R14,R12,12(R13)    SAVE REGISTROS TAREA ANTERIOR
.SA1     ANOP
&CAP     SETA  0
         AIF   ('&CAPPING' NE 'SI').LB01AA
&CAP     SETA  1
         LA    R&R(1),*+6          CAPPING
         BSM   R14,R&R(1)          CAPPING
         AIF   ('&SAVE' NE 'SI').LB01AA
         STCM  R14,B'1111',12(R13) CAPPING
.LB01AA  ANOP
         DROP  R15
         LR    R2,R1              R2 DIRECCIONA LISTA PARM
         LR    R&R(1),R15         DIRECCIONAR PRIMER REG.BASE
         USING &NOMBRE,R&R(1)     USING A NOMBRE DE PROGRAMA
.* ------------------------- REGISTROS BASE ADICIONALES
.A0      ANOP
&INIA    SETA  1
&INID    SETA  0
.A1      ANOP
&INIA    SETA  &INIA+1
&INID    SETA  &INID+4096
         AIF   (&INIA GT N'&R).FIN1
         USING &NOMBRE+&INID,R&R(&INIA)
         LA    R&R(&INIA),2048(,R&R(&INIA-1))
         LA    R&R(&INIA),2048(,R&R(&INIA))
         AGO   .A1
.* --------------------------------
.FIN1    ANOP
         TITLE '@@&NOMBRE @@ &INIC '
.*
         AIF   ('&TIPO' EQ 'RENT').LB01
.*
         AIF   ('&SAC' NE '1').LB0D
         MVI   CR,X'00'           CODIGO DE RETORNO A CEROS
         LA    R1,SAVE            ENCADENAMIENTO
         AGO   .LB0C
.LB0D    ANOP
         MVI   CR&SAC,X'00'       CODIGO DE RETORNO A CEROS
         LA    R1,SAVE&SAC        ENCADENAMIENTO
.LB0C    ANOP
         ST    R13,4(,R1)         STANDARD
         AIF   ('&SAVE' NE 'SI').SA2
         ST    R1,8(,R13)         DE LAS
.SA2     ANOP
         LR    R13,R1             SAVE AREAS
         AGO   .FIN
.*
.LB01    ANOP
         USING &PDIN,R13          USING AREA DINAMICA CON R13
.* - - - - - - - - - - - - - - - - -
         L     R15,=A(@LGDIN&SAC)  LONGITUD DEL AREA DINAMICA
&SBA     SETA  1
         AIF   ('&SUBPOOL' EQ '').LB02
         AIF   (&SUBPOOL LE 127).LB02A
         AIF   ('&AUTH' EQ 'NO').LB02A
&SBA     SETA  0
         AUTORIZA AUTHON
         MODESET  KEY=ZERO,MODE=SUP
.LB02A   ANOP
         AIF   ('&LOC' EQ '').LB02B
         L     R15,=A(@LGDIN&SAC)  LONGITUD DEL AREA DINAMICA
         GETMAIN R,LV=(15),SP=&SUBPOOL,LOC=&LOC PET.EN SUBPOOL INDICADO
         AGO   .LB02C
.LB02B   ANOP
         L     R15,=A(@LGDIN&SAC)  LONGITUD DEL AREA DINAMICA
         GETMAIN R,LV=(15),SP=&SUBPOOL PET. EN SUBPOOL INDICADO
.LB02C   ANOP
&SBP     SETA  &SUBPOOL
         AGO   .LB03
.LB02    ANOP
         AIF   ('&LOC' EQ '').LB02D
         L     R15,=A(@LGDIN&SAC)  LONGITUD DEL AREA DINAMICA
         GETMAIN R,LV=(15),LOC=&LOC GETMAIN SIN SUBPPOL EXPLICITO
         AGO   .LB03
.LB02D   ANOP
         L     R15,=A(@LGDIN&SAC)  LONGITUD DEL AREA DINAMICA
         GETMAIN R,LV=(15)         GETMAIN SIN SUBPPOL EXPLICITO
.LB03    ANOP
         AIF   ('&ERRGET' EQ '').ERRG1
.*
         LTR   R15,R15             GETMAIN CORRECTA ?
         BNZ   &ERRGET             NO, BIFURCACION A LABEL DE USUARIO
.*
         LR    R4,R1               CEROS BINARIOS
         L     R5,=A(@LGDIN&SAC)   LONGITUD DEL AREA DINAMICA
         SR    R15,R15             DINAMICA
         MVCL  R4,R14              OBTENIDA
.*
         LR    R15,R13             ENCADENAMIENTO
         LR    R13,R1              STANDARD
         ST    R15,4(R13)          DE LAS
         AIF   ('&SAVE' NE 'SI').FIN
         ST    R13,8(R15)          SAVE AREAS
         AGO   .FIN
.NOMBRE1 MNOTE 255,'FALTA NOMBRE PARA CSECT. '
         MEXIT
.NOMBRE2 MNOTE 255,'PRIMERA POSICION NOMBRE ERROR '
         MEXIT
.NOMBRE3 MNOTE 255,'EXCESIVA LONGITUD DE NOMBRE. '
         MEXIT
.R1      MNOTE 255,'EXCESIVO NUMERO DE REGISTROS BASE. '
         MEXIT
.R2      MNOTE 255,'FALTA AL MENOS UN REGISTRO BASE. '
         MEXIT
.R3      MNOTE 255,'REGISTROS BASE NO NUMERICOS. '
         MEXIT
.SUBPOO1 MNOTE 255,'LONGITUD SUBPOOL INCORRECTA (TRES POSICIONES). '
         MEXIT
.SUBPOO2 MNOTE 255,'SUBPOOL INCORRECTO (NO NUMERICO). '
         MEXIT
.SUBPOO3 MNOTE 255,'SUBPOOL INCORRECTO (SUPERIOR A 255).'
         MEXIT
.TIPO1   MNOTE 255,'TIPO INCORRECTO (ADMITIDOS RENT/NORENT). '
         MEXIT
.ERRG1   MNOTE 255,'FALTA LABEL PARA ERRORES EN GETMAIN. '
         MEXIT
.COMEN1  MNOTE 255,'EXCESIVA LONGITUD DE COMENTARIO. (40 CHAR MAX.) '
         MEXIT
.COMEN2  MNOTE 255,'EL COMENTARIO DEBER IR ENTRE COMILLAS. '
         MEXIT
.PDIN1   MNOTE 255,'FALTA NOMBRE DE DSECT PRINCIPIO AREA DINAMICA. '
         MEXIT
.FDIN1   MNOTE 255,'FALTA NOMBRE DE LA EQU DE FIN DE AREA DINAMICA. '
         MEXIT
.FIN     ANOP
         ACTR  12
         MEND
